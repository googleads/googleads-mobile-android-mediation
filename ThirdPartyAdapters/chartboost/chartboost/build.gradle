plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

/**
 * Extra properties used by the project.
 */
ext {
    // String property to store version name.
    stringVersion = "7.3.1.0"
    // String property to store group id.
    stringGroupId = "com.google.ads.mediation"
}

android {
    lintOptions {
        abortOnError false
    }

    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 70310
        versionName stringVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // Adding Chartboost SDK as a compileOnly dependency makes sure that the jar is not included in the
    // AAR.
    compileOnly files('libs/chartboost.jar')
    implementation 'com.google.android.gms:play-services-ads:17.1.1'
    implementation "com.google.android.gms:play-services-base:16.1.0"
    implementation "com.google.android.gms:play-services-ads-identifier:16.0.0"
}

/**
 * Jar task to make a sources jar.
 */
task sourcesJar (type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

/**
 * Publish closure consisting of maven publications that will be published using bintrayUpload.
 */
publishing {
    publications {
        chartboostPublications(MavenPublication) {
            groupId = stringGroupId
            artifactId = project.name
            version = stringVersion

            // Add the aar artifact to publication.
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar") {
                builtBy build
            }

            // Add the sources jar artifact to the publication.
            artifact(sourcesJar)
        }
    }
}

/**
 * Bintray closure needed to run the buntrayUpload task.
 *
 * Usage:
 * ./gradlew bintrayUpload -PbintrayUser=YOUR_BINTRAY_USER_ID -PbintrayApiKey=YOUR_BINTRAY_API_KEY
 *
 * The Bintray User ID and API key can be added to your system environment variables as BINTRAY_USER
 * and BINTRAY_API_KEY respectively, and the command could be reduced to:
 * ./gradlew bintrayUpload
 */
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser')
            : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey')
            : System.getenv('BINTRAY_API_KEY')
    publications = ['chartboostPublications']

    dryRun = false // Deploy after running.
    publish = false // Don't auto publish after deploying.

    pkg {
        repo = 'mobile-ads-adapters-android'
        name = "${stringGroupId}.${project.name}" // Display name for the package.
        userOrg = 'google'
        desc = 'Chartboost adapter for Google Mobile Ads mediation.'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://firebase.google.com/docs/admob/android/mediation'
        vcsUrl = 'https://github.com/googleads/googleads-mobile-android-mediation.git'
        issueTrackerUrl = 'https://github.com/googleads/googleads-mobile-android-mediation/issues'
        githubRepo = 'googleads/googleads-mobile-android-mediation'
        githubReleaseNotesFile = 'ThirdPartyAdapters/chartboost/CHANGELOG.md'
        version {
            name = stringVersion
        }
    }
}

/**
 * Add dependency tasks for bintrayUpload. The project needs to be built before bintrayUpload
 * task is run.
 */
bintrayUpload.dependsOn(clean, build)
bintrayUpload.mustRunAfter build
build.mustRunAfter clean
